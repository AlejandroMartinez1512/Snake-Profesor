' Gambas class file

'####################################################

'ESTADO - ATRIBUTOS

'####################################################


' es un conjunto de cuadrados ordenado, en forma de lista, donde
' el primero de la lista queremos que sea la cabeza
Private miCuerpo As Cuadrado[]

'tengo que saber en cada momento si me muevo a la I, D, ARR, AB
Private direccion As String


'####################################################

'COMPORTAMIENTO - MÉTODOS

'####################################################

'Constructor
'primero ponemos public, después sub, _new para los constructores
'después entre paréntesis tantos tripletes de parámetros como atributos (norma general)

Public Sub _new(nuevaX As Short, nuevaY As Short, colorSerpiente As Integer, nuevaDir As String)


     Dim cabeza As Cuadrado
     
     cabeza = New Cuadrado(colorSerpiente, 20, nuevaX, nuevaY)
     '¿cuál será el "ancho" de mi serpiente?
     
     ' Nos han definido la serpiente como de 20px
     
     
     'Preparo la lista de cuadrados
     miCuerpo = New Cuadrado[]
     
     'añadir la cabeza al cuerpo
     miCuerpo.Add(cabeza)
     
     direccion = nuevaDir
      
      
End

'En este bloque de código queremos conseguir que la serpiente se mueva
Public Sub moverse()
  'Hacemos que la serpiente crezca
  Me.crecer()
  
  'Eliminamos la última posición de la fila
  miCuerpo.Pop()
  
End



'En este bloque hacemos que se pinte la serpiente en el DrawingArea
Public Sub pintarse(tablero As DrawingArea)

  Dim iContador As Integer
  'tengo que conseguir que cada uno de los cuadrados, uno a uno, se pinte
  For iContador = 0 To miCuerpo.Count - 1
      miCuerpo[iContador].pintarse(tablero)
  Next
    
End

'En este bloque conseguimos que la serpiente crezca
Public Sub crecer() 
  'Coger un nuevo cuadrado. 
  Dim nuevaCabeza As Cuadrado
  Dim cabezaAntigua As Cuadrado
  
  
  cabezaAntigua = miCuerpo.First
  'crearlo y ponerlo donde está la cabeza.   
  nuevaCabeza = New Cuadrado(cabezaAntigua.getColor(), 20, cabezaAntigua.getPosX(), cabezaAntigua.getPosY())
   
  'Añadirlo al cuerpo
  miCuerpo.Add(nuevaCabeza, 0)
  
  'Moverlo un paso adelante.
  nuevaCabeza.moverse(direccion)
  
  
  
End

'Aquí conseguimos que la serpiente detecte si aun está viva... o no
Public Function estasMuerta(iAncho As Integer, iAlto As Integer) As Boolean
  
   Dim bResultado As Boolean
   Dim cabezaSerpiente As Cuadrado
   Dim iContador As Integer
   
   
   cabezaSerpiente = miCuerpo[0]
   bResultado = False
   
   'si mi cabeza tiene un x menor que 0, estoy muerta
   'si mi cabeza tiene un x mayor que el ancho, estoy muerta
   'si mi cabeza tiene un y menor que 0, estoy muerta
   'si mi cabeza tiene un y mayor que el alto, estoy muerta
   If cabezaSerpiente.getPosX() < 0 Or cabezaSerpiente.getPosY() < 0 Or
      cabezaSerpiente.getPosX() > iAncho Or cabezaSerpiente.getPosY() > iAlto Then
      bResultado = True
   Endif
   
   
   
   'ademas
   'si para cada cuadrado de mi cuerpo de posicion quinta (4) o mayor
   'la x de ese cuadrado y la y de ese cuadrado son exactamente = que 
   'los de mi cabeza, estoy muerta.
   For iContador = 4 To miCuerpo.Count - 1 
      If (cabezaSerpiente.colisiona(miCuerpo[iContador])) Then
         bResultado = True
      Endif
   Next
  
  'si estoy muerta es porque bResultado = True
  
   Return bResultado
  
End

'Aqui hacemos que la serpiente sapa cambiar de dirección
Public Sub cambiaDir(letra As String)
  
  
  'si letra es W,A,S,D y ninguna más, entonces...
  If letra = "W" Or letra = "A" Or letra = "S" Or letra = "D" Then
       direccion = letra
  Endif
  
  
End


'getters

